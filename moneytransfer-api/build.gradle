plugins {
	id 'org.springframework.boot' version '2.1.9.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id 'findbugs'
}

ext {
    iban4jVersion = '3.2.1'
    h2Version = '1.4.197'
    mysqlConnectorVersion = '8.0.18'
    checkstyleVersion = '6.16.1'
    findbugsVersion = '3.0.1'
}

sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

bootJar {
    // https://docs.spring.io/spring-boot/docs/current/reference/html/deployment.html#deployment-install
    launchScript()
    archiveFileName = 'moneytransfer.jar'
}

jacoco {
    toolVersion = "0.8.5"
}
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}
test.finalizedBy(jacocoTestReport)

checkstyle {
    toolVersion = checkstyleVersion
    config = rootProject.resources.text.fromFile('config/checkstyle.xml')
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

findbugs {
    ignoreFailures = false
    toolVersion = findbugsVersion
    includeFilter = file("$rootProject.projectDir/config/findbugsIncludeFilter.xml")
    effort = 'max'
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}
task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}
check.dependsOn integrationTest

dependencies {
    compile             group: 'org.iban4j', name: 'iban4j', version: iban4jVersion

    testCompile         group: 'com.h2database', name: 'h2', version: h2Version
    runtimeOnly         group: 'mysql', name: 'mysql-connector-java', version: mysqlConnectorVersion

    implementation      'org.springframework.boot:spring-boot-starter-web'
    implementation      'org.springframework.boot:spring-boot-starter-log4j2'
    implementation      'org.springframework.boot:spring-boot-starter-actuator'
    implementation      'org.springframework.boot:spring-boot-starter-jdbc'
    implementation      'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation  'org.springframework.boot:spring-boot-starter-test'
}

configurations {
    all*.exclude module : 'spring-boot-starter-logging'
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}